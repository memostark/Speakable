apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.guillermonegrete.tts"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 2
        versionName "1.13.0"
        testInstrumentationRunner "com.guillermonegrete.tts.CustomTestRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.1'
    }

    namespace 'com.guillermonegrete.tts'

}

repositories {
    flatDir {
        dirs 'libs'
        google()
        maven { url 'https://dl.bintray.com/android/android-tools' }
        maven { url 'https://kotlin.bintray.com/kotlinx/' }
    }
    mavenCentral()
    maven() {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven { url 'https://jitpack.io' }
}

ext {
    supportLibVersion = '26.1.0'
}

kapt {
    generateStubs = true
    // Required by hilt: https://dagger.dev/hilt/gradle-setup
    correctErrorTypes = true
}

dependencies {

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    //  Mockito framework
    testImplementation 'org.mockito:mockito-core:2.25.1'
    testImplementation group: 'com.nhaarman', name: 'mockito-kotlin', version: '1.5.0'
    testImplementation "io.mockk:mockk:1.12.0"

    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.01.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.activity:activity-compose:1.6.1'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // fragments
    def fragment_version = "1.5.0" // Newest versions cause a memory leak, update when fixed.
    implementation "androidx.fragment:fragment:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // KTX core
    implementation "androidx.core:core-ktx:1.2.0"

    // Google custom tabs
    implementation 'androidx.browser:browser:1.0.0'

    // Firebase OCR
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.2'

    // Firebase language detection
    implementation 'com.google.firebase:firebase-ml-natural-language:19.0.1'
    implementation 'com.google.firebase:firebase-ml-natural-language-language-id-model:19.0.1'

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'

    // Room
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Lifecycle-aware components
    def lifecycle_version = "2.5.0"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Coroutines
    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Navigation component
    def nav_version = "2.5.3"
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Apache IO utils
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'

    // Database visualizer
    debugImplementation 'com.github.amitshekhariitbhu.Android-Debug-Database:debug-db:v1.0.6'

    // Dagger 2
    def dagger_version = "2.29.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Hilt
    def hilt_version = "2.38.1"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Hilt testing
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"

    lintChecks "com.google.dagger:dagger-lint-aar:$dagger_version"

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // Jsoup HTML parser
    implementation 'org.jsoup:jsoup:1.13.1'

    // Okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))
    implementation("com.squareup.okhttp3:logging-interceptor")
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"

    // Timber logger
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Instrumental testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test:rules:1.4.0'
    def espressoVersion = "3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // Need to use exclude due to a bug. More info here: https://github.com/android/android-test/issues/731#issuecomment-687201783
    debugImplementation("androidx.fragment:fragment-testing:$fragment_version") {
        exclude group: 'androidx.test', module: 'monitor'
    }

    // Glide Image Library
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // How to use app library
    implementation ('com.github.worker8:tourguide:2.0.0-SNAPSHOT@aar') {
        transitive=true
    }
}

apply plugin: 'com.google.gms.google-services'

